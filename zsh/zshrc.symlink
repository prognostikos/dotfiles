### ENV

export EDITOR=vim
export LANG=en_US.UTF-8
export LC_ALL=$LANG
export CLICOLOR=true

export GIT_PS1_SHOWDIRTYSTATE=true
export GIT_PS1_SHOWUNTRACKEDFILES=true
export WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'

if [ ! -d /Volumes/secrets ]; then
  hdiutil attach ~/Documents/secrets.sparseimage
fi

### PATH manipulation

if command -v rbenv > /dev/null; then
  eval "$(rbenv init - zsh)"
fi

if command -v direnv > /dev/null; then
  eval "$(direnv hook zsh)"
fi

if [ -d ~/c/go ]; then
  export GOPATH=~/c/go
  export PATH="$GOPATH/bin:$PATH"
fi

export PATH="/usr/local/foreman/bin:.git/safe/../../bin:$HOME/.dotfiles/bin:$PATH"

# add homebrew's completions to fpath
fpath=(/usr/local/share/zsh/site-functions $fpath)

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# pull in config files
for config_file ($ZSH/**/*.zsh) source $config_file

# pull in all aliases
for aliases_file ($ZSH/**/aliases) source $aliases_file

# Act like emacs even though Vim is my editor
bindkey -e

# Always pushd when changing directory
setopt AUTO_PUSHD
setopt PUSHD_IGNORE_DUPS

# set up bash-style backward kill
autoload -U select-word-style
select-word-style bash

# use C-s for opening in vertical splits w/command-t, not capturing output
stty -ixon

# Turn on completions (`brew install zsh-completions` for even more)
autoload -U compinit
compinit
zmodload -i zsh/complist

# Speed up path completion
zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' use-cache on

# Ignore some files when completing
fignore=(.o .class .ctxt .obj)

# pull in all custom completion.sh files
for completion_file ($ZSH/**/completion.sh) source $completion_file

# run this after reattaching to a detached tmux session to update the ssh-agent forwarding config
fixssh() {
  for key in SSH_AUTH_SOCK SSH_CONNECTION SSH_CLIENT; do
    if (tmux show-environment | grep "^${key}" > /dev/null); then
      value=`tmux show-environment | grep "^${key}" | sed -e "s/^[A-Z_]*=//"`
      export ${key}="${value}"
    fi
  done
}

# the awscli completion seems to need explicit sourcing - fpath is not enough
if command -v aws > /dev/null ; then
  source /usr/local/share/zsh/site-functions/_aws
fi

# use ssh-agent
if [ -z $SSH_AGENT_PID ]; then
  eval $(ssh-agent)
  ssh-add
fi

# use gpg-agent
if ! pgrep -x -u "${USER}" gpg-agent >/dev/null 2>&1; then
  gpg-connect-agent /bye >/dev/null 2>&1
fi

export GPG_TTY=$(tty)
