### ENV

if command -v nvim > /dev/null; then
  export EDITOR=nvim
  alias vi=nvim
else
  export EDITOR=vim
fi

export LANG=en_US.UTF-8
export LC_ALL=$LANG
export CLICOLOR=true

export GIT_PS1_SHOWDIRTYSTATE=true
export GIT_PS1_SHOWUNTRACKEDFILES=true
export WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'

export CDPATH=$HOME/c/bn:$HOME/c:$HOME/b:$HOME:d

if command -v hdiutil > /dev/null; then
  if [ ! -d /Volumes/secrets ]; then
    if [ -f ~/Documents/secrets.sparseimage ]; then
      hdiutil attach ~/Documents/secrets.sparseimage
    fi
  fi
fi

### PATH manipulation

if [ "$(uname -p)" = "arm" ]; then
  BREW_PREFIX="/opt/homebrew"
else
  BREW_PREFIX="/usr/local"
fi

if [ -d $BREW_PREFIX ]; then
  export PATH="$BREW_PREFIX/bin:$BREW_PREFIX/sbin:$PATH"
fi

if command -v rbenv > /dev/null; then
  eval "$(rbenv init - zsh)"
fi

if command -v direnv > /dev/null; then
  eval "$(direnv hook zsh)"
fi

if command -v mise > /dev/null; then
  eval "$(mise activate zsh)"
fi

if [ -d ~/c/go ]; then
  export GOPATH=~/c/go
  export PATH="$GOPATH/bin:$PATH"
fi

export PATH="$HOME/.dotfiles/bin:$PATH"

# add homebrew's completions to fpath
fpath=($BREW_PREFIX/share/zsh/site-functions $fpath)

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# pull in config files
for config_file ($ZSH/**/*.zsh) source $config_file

# pull in all aliases
for aliases_file ($ZSH/**/aliases) source $aliases_file

# Act like emacs even though Vim is my editor
bindkey -e

# Always pushd when changing directory
setopt AUTO_PUSHD
setopt PUSHD_IGNORE_DUPS

# set up bash-style backward kill
autoload -U select-word-style
select-word-style bash

# use C-s for opening in vertical splits w/command-t, not capturing output
stty -ixon

# Turn on completions (`brew install zsh-completions` for even more)
autoload -U compinit

# Only rebuild completions once a day
if [[ -z $(find ~/.zcompdump -mtime 0 2>/dev/null) ]]; then
  compinit
  touch ~/.zcompdump
else
  compinit -C
fi

zmodload -i zsh/complist

# Ignore some files when completing
fignore=(.o .class .ctxt .obj)

# pull in all custom completion.sh files
for completion_file ($ZSH/**/completion.sh) source $completion_file

# Speed up path completion
zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' rehash true

# run this after reattaching to a detached tmux session to update the ssh-agent forwarding config
fixssh() {
  for key in SSH_AUTH_SOCK SSH_CONNECTION SSH_CLIENT; do
    if (tmux show-environment | grep "^${key}" > /dev/null); then
      value=`tmux show-environment | grep "^${key}" | sed -e "s/^[A-Z_]*=//"`
      export ${key}="${value}"
    fi
  done
}

# the awscli completion seems to need explicit sourcing - fpath is not enough
if command -v aws > /dev/null ; then
  if [ -f $BREW_PREFIX/share/zsh/site-functions/_aws ]; then
    source $BREW_PREFIX/share/zsh/site-functions/_aws
  fi
fi

# use gpg-agent
if ! pgrep -x -u "${USER}" gpg-agent >/dev/null 2>&1; then
  gpg-connect-agent /bye >/dev/null 2>&1
fi

export GPG_TTY=$(tty)

if [ -f $HOME/.cacert.pem ]; then
  export CURL_CA_BUNDLE=$HOME/.cacert.pem
fi

if [[ "$OSTYPE" == "darwin"* ]]; then
  export OBJC_DISABLE_INITIALIZE_FORK_SAFETY=YES
fi

export HOMEBREW_NO_ENV_HINTS=1

# deduplicate path entries
typeset -U path
